agent_of_checking_existence_of_any_main_identifier
=> nrel_main_idtf:
	[агентная scp-программа проверки наличия sc-элементов, имеющих системный идентификатор, но не имеющих ни одного основного в рамках заданной структуры] (* <- lang_ru;; *);
	[agent scp-program of checking existence of any main identifiers for sc-elements within the given structure] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_checking_existence_of_any_main_identifier = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_checking_existence_of_any_main_identifier_operator0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator1;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_find_sc_elements_without_any_main_identifier;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator2;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator3;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_struct:: _param;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator4;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator5;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator3 (*
		_<- return;;

	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator4 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _common_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _context;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc_in_relation;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_context_of_action;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator6;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator7;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator5 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_checking_existence_of_any_main_identifier_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong 1st parameter! It must be structure.];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator8;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator8 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator3;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator6 (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator9;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator7 (*
		_<- contAssign;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [false];;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator9;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator9 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: rrel_struct:: _incorrect_structure;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator10;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator10 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_node:: rrel_scp_var:: _node1;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _allRelations;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator12;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator11 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _allRelations;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _someRelation;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator13;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator12;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator12 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_generation_of_verification_answer;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_checking_existence_of_any_main_identifier_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: action_to_find_sc_elements_without_any_main_identifier;;
			_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _incorrect_structure;;
			_-> rrel_4:: rrel_fixed:: rrel_scp_const:: structure_which_elements_have_sys_idtf_but_dont_have_main_idtf_in_all_lang;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator14;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator14 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator3;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator13 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc1;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator15;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator15 (*
		_<- stringIfEq;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _is_context_exist;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [true];;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator16;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator17;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator16 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _someRelation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node3;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_system_identifier;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _allSystemIdtfs;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator18;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator17 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _someRelation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node3;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_system_identifier;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator19;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator19 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _someRelation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc3;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node4;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_main_idtf;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator20;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator20 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _incorrect_structure;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc5;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _someRelation;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_key_sc_element;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator18 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _allSystemIdtfs;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _someSystemIdtf;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator21;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator22;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator21 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator23;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator22 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _allSystemIdtfs;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator23 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _someSystemIdtf;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator24;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator18;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator24 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _allSystemIdtfs;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator25;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator25 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _someRelation;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node3;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_main_idtf;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _allMainIdtfs;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator26;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator20;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator26 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _allMainIdtfs;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _someMainIdtf;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator27;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator28;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator27 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator29;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator28 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _allMainIdtfs;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator20;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator29 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _context;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _someMainIdtf;;

		_=> nrel_then:: .._agent_of_checking_existence_of_any_main_identifier_operator30;;
		_=> nrel_else:: .._agent_of_checking_existence_of_any_main_identifier_operator26;;
	*);;

	_-> ...._agent_of_checking_existence_of_any_main_identifier_operator30 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _allMainIdtfs;;

		_=> nrel_goto:: .._agent_of_checking_existence_of_any_main_identifier_operator11;;
	*);;

*);;

*];;
