agent_of_finding_all_statements_associated_with_given_entity
=> nrel_main_idtf:
	[агентная scp-программа поиска всех утверждений, ассоциированных с заданной сущностью] (* <- lang_ru;; *);
	[agent scp-program of finding of all statements, associated with a given entity] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_finding_all_statements_associated_with_given_entity = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_finding_all_statements_associated_with_given_entity_operator0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator1;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_find_all_statements_associated_with_given_entity;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_finding_all_statements_associated_with_given_entity_operator2;;
		_=> nrel_else:: .._agent_of_finding_all_statements_associated_with_given_entity_operator3;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_node:: rrel_scp_var:: _param;;

		_=> nrel_then:: .._agent_of_finding_all_statements_associated_with_given_entity_operator4;;
		_=> nrel_else:: .._agent_of_finding_all_statements_associated_with_given_entity_operator5;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator3 (*
		_<- return;;

	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator4 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: rrel_struct:: _answer_of_finding_of_all_statements;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator6;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator5 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_adding_validating_message;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_all_statements_associated_with_given_entity_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_const:: [Wrong parameter! It must be node];;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator7;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator7 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator3;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator6 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_finding_associated_statements;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_all_statements_associated_with_given_entity_param1 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _answer_of_finding_of_all_statements;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator8;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator8 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr2;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator9;;
	*);;

	_-> ...._agent_of_finding_all_statements_associated_with_given_entity_operator9 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_of_finding_of_all_statements;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_of_finding_all_statements_associated_with_given_entity_operator3;;
	*);;

*);;

*];;
