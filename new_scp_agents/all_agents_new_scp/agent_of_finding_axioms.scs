agent_of_finding_axioms
=> nrel_main_idtf:
	[агентная scp-программа поиска аксиом заданной sc-онтологии] (* <- lang_ru;; *);
	[agent scp-program of finding axioms for given sc-ontology] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_finding_axioms = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_finding_axioms_operator0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator1;;
	*);;

	_-> ...._agent_of_finding_axioms_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finding_axioms;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_finding_axioms_operator2;;
		_=> nrel_else:: .._agent_of_finding_axioms_operator3;;
	*);;

	_-> ...._agent_of_finding_axioms_operator2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _elem;;

		_=> nrel_then:: .._agent_of_finding_axioms_operator4;;
		_=> nrel_else:: .._agent_of_finding_axioms_operator3;;
	*);;

	_-> ...._agent_of_finding_axioms_operator3 (*
		_<- return;;

	*);;

	_-> ...._agent_of_finding_axioms_operator4 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _answer_finding_axiom;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator5;;
	*);;

	_-> ...._agent_of_finding_axioms_operator5 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _elem;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _el2;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_axiom;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _all_statements;;

		_=> nrel_then:: .._agent_of_finding_axioms_operator6;;
		_=> nrel_else:: .._agent_of_finding_axioms_operator3;;
	*);;

	_-> ...._agent_of_finding_axioms_operator6 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _elem;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _elem2;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_axiom;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_3:: rrel_set_4:: _answer_finding_axiom;;

		_=> nrel_then:: .._agent_of_finding_axioms_operator7;;
		_=> nrel_else:: .._agent_of_finding_axioms_operator3;;
	*);;

	_-> ...._agent_of_finding_axioms_operator7 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _all_statements;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _nessecary_vertex;;

		_=> nrel_then:: .._agent_of_finding_axioms_operator8;;
		_=> nrel_else:: .._agent_of_finding_axioms_operator9;;
	*);;

	_-> ...._agent_of_finding_axioms_operator8 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator10;;
	*);;

	_-> ...._agent_of_finding_axioms_operator9 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: sc_ontology;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _elem;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: _answer_finding_axiom;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator11;;
	*);;

	_-> ...._agent_of_finding_axioms_operator11 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_finding_axiom;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _elem;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator12;;
	*);;

	_-> ...._agent_of_finding_axioms_operator12 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_finding_axiom;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: sc_ontology;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator13;;
	*);;

	_-> ...._agent_of_finding_axioms_operator13 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_finding_axiom;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: rrel_axiom;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator14;;
	*);;

	_-> ...._agent_of_finding_axioms_operator14 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _all_statements;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator15;;
	*);;

	_-> ...._agent_of_finding_axioms_operator15 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_finding_axiom;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator3;;
	*);;

	_-> ...._agent_of_finding_axioms_operator10 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _nessecary_vertex;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _smth;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_main_idtf;;

		_=> nrel_then:: .._agent_of_finding_axioms_operator16;;
		_=> nrel_else:: .._agent_of_finding_axioms_operator7;;
	*);;

	_-> ...._agent_of_finding_axioms_operator16 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_finding_identifier;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_finding_axioms_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _nessecary_vertex;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _answer_finding_axiom;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator17;;
	*);;

	_-> ...._agent_of_finding_axioms_operator17 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_finding_axioms_operator7;;
	*);;

*);;

*];;
