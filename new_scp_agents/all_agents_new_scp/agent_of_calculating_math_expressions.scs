agent_of_calculating_math_expressions
=> nrel_main_idtf:
	[агентная scp-программа вычисления математических выражений] (* <- lang_ru;; *);
	[agent scp-program of calculating math expressions] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_calculating_math_expressions = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_calculating_math_expressions_operator0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator1;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_of_calculating_math_expressions;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_calculating_math_expressions_operator2;;
		_=> nrel_else:: .._agent_of_calculating_math_expressions_operator3;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _block;;

		_=> nrel_then:: .._agent_of_calculating_math_expressions_operator4;;
		_=> nrel_else:: .._agent_of_calculating_math_expressions_operator3;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator3 (*
		_<- return;;

	*);;

	_-> ...._agent_of_calculating_math_expressions_operator4 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _final;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator5;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator5 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_of_calculating_math_expressions;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._agent_of_calculating_math_expressions_param0 (*
			_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _block;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _final;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator6;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator6 (*
		_<- waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr1;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator7;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator7 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _final;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _result;;

		_=> nrel_then:: .._agent_of_calculating_math_expressions_operator8;;
		_=> nrel_else:: .._agent_of_calculating_math_expressions_operator9;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator8 (*
		_<- printEl;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _result;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator10;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator9 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finished_unsuccessfully;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator3;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator10 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _result;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator11;;
	*);;

	_-> ...._agent_of_calculating_math_expressions_operator11 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: question_finished_successfully;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc3;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_calculating_math_expressions_operator3;;
	*);;

*);;

*];;
