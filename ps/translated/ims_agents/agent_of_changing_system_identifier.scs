agent_of_changing_system_identifier
=> nrel_main_idtf:
	[агентная scp-программа замены системного идентификатора на указанный файл] (* <- lang_ru;; *);
	[agent scp-program of changing system identifier on specified file] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_changing_system_identifier = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;;
_-> rrel_2:: rrel_in:: _input_arc;;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_changing_system_identifier_operator0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator1;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_change_system_identifier;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_changing_system_identifier_operator2;;
		_=> nrel_else:: .._agent_of_changing_system_identifier_operator3;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator2 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _param1;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_1;;

		_=> nrel_then:: .._agent_of_changing_system_identifier_operator4;;
		_=> nrel_else:: .._agent_of_changing_system_identifier_operator3;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator3 (*
		_<- return;;

	*);;

	_-> .._agent_of_changing_system_identifier_operator4 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _param2;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_2;;

		_=> nrel_then:: .._agent_of_changing_system_identifier_operator5;;
		_=> nrel_else:: .._agent_of_changing_system_identifier_operator3;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator5 (*
		_<- genEl;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: _answer_of_change_system_element;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator6;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator6 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param1;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc_remove;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _node_change;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_system_identifier;;

		_=> nrel_then:: .._agent_of_changing_system_identifier_operator7;;
		_=> nrel_else:: .._agent_of_changing_system_identifier_operator3;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator7 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc_remove;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator8;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator8 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _node_change;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator9;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator9 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param1;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc_change;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param2;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc_nrel;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_system_identifier;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator10;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator10 (*
		_<- searchSetStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _param1;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc_change;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param2;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc_nrel;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_system_identifier;;
		_-> rrel_fixed:: rrel_scp_var:: rrel_set_2:: rrel_set_4:: _answer_of_change_system_element;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator11;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator11 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_change_system_element;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param1;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator12;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator12 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_change_system_element;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _param2;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator13;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator13 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _answer_of_change_system_element;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: nrel_system_identifier;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator14;;
	*);;

	_-> .._agent_of_changing_system_identifier_operator14 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer_of_change_system_element;;
		_-> rrel_4:: rrel_assign:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_of_changing_system_identifier_operator3;;
	*);;

*);;

*];;
