agent_of_removing_elements_of_the_struct
=> nrel_main_idtf:
	[агентная scp-программа удаления элементов структуры] (* <- lang_ru;; *);
	[agent scp-program of removing elements of the struct] (* <- lang_en;; *);
<- scp_program;
<- agent_scp_program;
-> rrel_key_sc_element: .._process1;;

agent_of_removing_elements_of_the_struct = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: _input_arc;
_<= nrel_decomposition_of_action:: .._actions (*

	_-> rrel_1:: .._agent_of_removing_elements_of_the_struct_operator0 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _input_arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator1;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator1 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: action_to_removing_elements_of_the_struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc1;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _quest;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator2;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator3;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator2 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _struct;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator4;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator3 (*
		_<- return;;

	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator4 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _struct;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_node:: rrel_scp_var:: _node;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator5;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator6;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator5 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator7;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator6 (*
		_<- genElStr3;;
		_-> rrel_1:: rrel_assign:: rrel_const:: rrel_node:: rrel_scp_var:: _answer;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _struct;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator8;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator8 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
		_-> rrel_2:: rrel_assign:: rrel_common:: rrel_const:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _answer;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_answer;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator3;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator7 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _outputTempNode;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_var:: _struct;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator9;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator10;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator9 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator7;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator10 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _inputTempNode;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc2;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_var:: _struct;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator11;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator12;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator11 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator10;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator12 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _outputTempNode;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_3:: _adjacentNodes;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator13;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator14;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator13 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _adjacentNodes;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _outputTempNode;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator15;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator14;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator14 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_scp_var:: _inputTempNode;;
		_-> rrel_2:: rrel_assign:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _node;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _adjacentNodes;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator16;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator17;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator16 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _adjacentNodes;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _inputTempNode;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator18;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator17;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator17 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _node;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator4;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator18 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator19;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator19 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: rrel_struct:: _tempStructs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _inputTempNode;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _setStructs;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator20;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator16;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator20 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _setStructs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _tempStruct;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator21;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator16;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator21 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator22;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator22 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: substantive_part_of_the_knowledge_base;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _tempStruct;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator4;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator20;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator15 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator23;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator23 (*
		_<- searchSetStr3;;
		_-> rrel_1:: rrel_assign:: rrel_node:: rrel_scp_var:: rrel_struct:: _tempStructs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _outputTempNode;;
		_-> rrel_assign:: rrel_scp_var:: rrel_set_1:: _setStructs;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator24;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator13;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator24 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _setStructs;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _tempStruct;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator25;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator13;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator25 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_erase:: rrel_fixed:: rrel_scp_var:: _arc;;

		_=> nrel_goto:: .._agent_of_removing_elements_of_the_struct_operator26;;
	*);;

	_-> .._agent_of_removing_elements_of_the_struct_operator26 (*
		_<- searchElStr3;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: substantive_part_of_the_knowledge_base;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: _arc;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_var:: _tempStruct;;

		_=> nrel_then:: .._agent_of_removing_elements_of_the_struct_operator4;;
		_=> nrel_else:: .._agent_of_removing_elements_of_the_struct_operator24;;
	*);;

*);;

*];;
