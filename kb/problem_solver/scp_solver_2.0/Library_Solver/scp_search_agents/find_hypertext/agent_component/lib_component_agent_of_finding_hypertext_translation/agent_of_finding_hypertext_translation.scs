agent_of_finding_hypertext_translation
=> nrel_main_idtf:
	[агентная scp-программа поиска гипертекстовой трансляции] (* <- lang_ru;; *);
	[agent scp-program of finding hypertext translation] (* <- lang_en;; *);

<- agent_scp_program;;

scp_program -> agent_of_finding_hypertext_translation 
	(*
	-> rrel_params: .agent_of_finding_hypertext_translation_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_finding_hypertext_translation_operator_set 
		(*
		-> rrel_init: .agent_of_finding_hypertext_translation_operator1A 
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
		  	-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

		  	=> nrel_goto: .agent_of_finding_hypertext_translation_operator1B;;
			*);;

		-> .agent_of_finding_hypertext_translation_operator1B 
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: question_of_finding_hypertext;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

		  	=> nrel_then: .agent_of_finding_hypertext_translation_operator1C1;;
		  	=> nrel_else: .agent_of_finding_hypertext_translation_operator_return;;
			*);;

		-> .agent_of_finding_hypertext_translation_operator1C1 
			(*
		  	<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _root_node;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

		  	=> nrel_then: .agent_of_finding_hypertext_translation_operator1C2;;
		  	=> nrel_else: .agent_of_finding_hypertext_translation_operator_return;;
			*);;

		-> .agent_of_finding_hypertext_translation_operator1C2 
			(*
		  	<- searchElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _current_lang;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

		  	=> nrel_then: .agent_of_finding_hypertext_translation_operator1C3;;
		  	=> nrel_else: .agent_of_finding_hypertext_translation_operator_return;;
			*);;

		-> .agent_of_finding_hypertext_translation_operator1C3
			(*
		 	<- searchElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_const: languages;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _current_lang;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator1D;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator_return;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator1D 
			(*
		 	<- genEl;;

                 	-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _answer;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator1E;;
		 	*);;

		-> .agent_of_finding_hypertext_translation_operator1E
			(*
		 	<- searchSetStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _root_node;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _hyper_node;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _hypertext_nodes;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator1F;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator2A;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator1F
			(*
		 	<- searchElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _hypertext_nodes;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _hypertext;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator1G;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator1F_erase;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator1F_erase 
			(*
		 	<- eraseEl;;

                 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _hypertext_nodes;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator2A;;
		 	*);;

		-> .agent_of_finding_hypertext_translation_operator1G 
			(*
		 	<- eraseEl;;

                 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator1H;;
		 	*);;

		-> .agent_of_finding_hypertext_translation_operator1H
			(*
		 	<- searchSetStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _current_lang;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _hypertext;;

			-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator3B;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator1F;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator2A
			(*
		 	<- searchSetStr5;;

		 	-> rrel_1: rrel_assign: rrel_scp_var: _tuple1;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _root_node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_sc_text_translation;;

			-> rrel_set_1: rrel_assign: rrel_scp_var: _set_of_translations;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator2B;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator_return;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator2B
			(*
		 	<- searchElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_translations;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _translation;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator2C;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator2B_erase;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator2B_erase 
			(*
		 	<- eraseEl;;

                 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_of_translations;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator_gen_answer;;
		 	*);;

		-> .agent_of_finding_hypertext_translation_operator2C 
			(*
		 	<- eraseEl;;

                 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc1;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator2D;;
		 	*);;

		-> .agent_of_finding_hypertext_translation_operator2D
			(*
		 	<- searchElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _translation;;
		 	-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _root_node;;
                 	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_sc_text_translation;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator2E;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator2B;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator2E
			(*
		 	<- searchElStr5;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _translation;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_assign: rrel_scp_var: _hypertext;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		 	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_example;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator2F;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator2B;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator2F
			(*
		 	<- searchSetStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _current_lang;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _hypertext;;

			-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;

                 	=> nrel_then: .agent_of_finding_hypertext_translation_operator2G;;
                 	=> nrel_else: .agent_of_finding_hypertext_translation_operator2B;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator2G 
			(*
		 	<- eraseEl;;

                 	-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _set_of_translations;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator3A;;
		 	*);;

		-> .agent_of_finding_hypertext_translation_operator3A 
			(*
			<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _root_node;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_const: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _hypertext;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator3B;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator3B
			(*
			<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _hypertext;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator3C;;
                 	*);;

		-> .agent_of_finding_hypertext_translation_operator3C 
			(*
			<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _root_node;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator3D;;
                 	*);;

		/*-> .agent_of_finding_hypertext_translation_operator3D 
			(*
			<- genElStr3;;

		 	-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
		 	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		 	-> rrel_3: rrel_fixed: rrel_scp_var: _current_lang;;

                 	=> nrel_goto: .agent_of_finding_hypertext_translation_operator3E;;
                 	*);;*/

		-> .agent_of_finding_hypertext_translation_operator3D 
			(*
		  	<- searchSetStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _root_node;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _hypertext;;

			-> rrel_set_2: rrel_fixed: rrel_scp_var: _answer;;

		  	=> nrel_goto: .agent_of_finding_hypertext_translation_operator_gen_answer;;
			*);;

		-> .agent_of_finding_hypertext_translation_operator_gen_answer 
			(*
			<- genElStr5;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
		  	-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
		  	-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

		  	=> nrel_goto: .agent_of_finding_hypertext_translation_operator_return;;
			*);;
					
		-> .agent_of_finding_hypertext_translation_operator_return 
			(*
		  	<- return;;
			*);;
		*);;
	*);;

























